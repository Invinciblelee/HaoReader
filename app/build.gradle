apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

static def getVersionName() {
    return String.format(Locale.getDefault(), "2.0.%d", (int) (System.currentTimeMillis() / 1000 / 60 / 60))
}

def getVersionCode() {// 获取版本号
    def versionFile = file('version.properties')// 读取第一步新建的文件
    if (versionFile.canRead()) {// 判断文件读取异常
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps['VERSION_CODE'].toInteger()// 读取文件里面的版本号
        def runTasks = gradle.startParameter.taskNames
        if ('assembleRelease' in runTasks) {//仅在assembleRelease任务是增加版本号,其他渠道包在此分别配置
            // 版本号自增之后再写入文件（此处是关键，版本号自增+1）
            versionProps['VERSION_CODE'] = (++versionCode).toString()
            versionProps.store(versionFile.newWriter(), null)
        }
        return versionCode // 返回自增之后的版本号
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

def name = "MONKOVEL"

def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyFile = file('Bookself.jks')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyFile = file(keystoreFilepath)
}



android {
    compileSdkVersion compile_sdk_version

    def currentVersionCode = getVersionCode()
    def currentVersionName = getVersionName()

    signingConfigs {
        myConfig {
            storeFile keyFile
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
        }
    }

    defaultConfig {
        applicationId "com.gedoor.monkeybook.super"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode currentVersionCode
        versionName currentVersionName
        project.ext.set("archivesBaseName", name + "_" + version)
        multiDexEnabled true

    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
            buildConfigField "boolean", "IS_RELEASE", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.myConfig
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
            buildConfigField "boolean", "IS_RELEASE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${name}_${defaultConfig.versionName}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "$support_library_version"
            }
        }
    }
}

dependencies {
    api project(':basemvplib')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'

    //support
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:support-compat:$support_library_version"
    implementation 'com.google.code.gson:gson:2.8.2'

    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.1.0'

    //codecMD5
    implementation 'commons-codec:commons-codec:1.11'

    //ProgressBar
    implementation 'com.zhangmonke:MProgressBar:1.0.1'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation 'jp.wasabeef:glide-transformations:3.2.0'
    // If you want to use the GPU Filters
    implementation 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'

    //AutoFitTextView
    implementation 'me.grantland:autofittextview:0.2.1'

    //删除粒子效果
    implementation 'tyrantgit:explosionfield:1.0.1'

    //View简易动画
    implementation 'com.daimajia.easing:library:2.1@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    //SwitchButton
    implementation 'com.kyleduo.switchbutton:library:2.0.0'

    //bind view
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //动画
    implementation 'com.victor:lib:1.0.4'

    //简易权限获取
    implementation 'pub.devrel:easypermissions:2.0.0'

    //二维码
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    //颜色选择
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'

    //简繁转换
    implementation 'com.luhuiguo:chinese-utils:1.0'

    implementation 'com.github.florent37:arclayout:1.0.3'
}

greendao {
    schemaVersion 57
    daoPackage 'com.monke.monkeybook.dao'
    targetGenDir 'src/main/java'
}

